; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=UFRaw
AppVerName=UFRaw @VERSION@
AppPublisher=Udi Fuchs
AppPublisherURL=http://ufraw.sourceforge.net/
AppSupportURL=http://ufraw.sourceforge.net/
AppUpdatesURL=http://ufraw.sourceforge.net/
DefaultDirName={reg:HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\WinGimp-2.0_is1,InstallLocation|{pf}\UFRaw}
DefaultGroupName={reg:HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\WinGimp-2.0_is1,Inno Setup: Icon Group|UFRaw}
AllowNoIcons=yes
Compression=lzma
SolidCompression=yes
@COMMENT_ICON@SetupIconFile=ufraw_icon.ico
UninstallDisplayIcon={app}\bin\ufraw.exe
DirExistsWarning=no
OutputBaseFileName=ufraw-@VERSION@-setup
OutputDir=.
WizardImageFile=ufraw-setup.bmp
WizardSmallImageFile=ufraw_icon.bmp
UsePreviousAppDir=no

[Tasks]
Name: "unplug"; Description: "Attempt to delete old versions of the UFRaw plug-in that were installed in the user's folders. This option only deletes ufraw-gimp.exe from the current user folders. You should make sure that there are no other copies of ufraw-gimp.exe in any of the user's folders that wish to use the new version of the UFRaw plug-in."; GroupDescription: "Delete previous versions:";
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}";
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "ufraw.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "ufraw-batch.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "@DOSPREFIX@\bin\liblcms-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "@DOSPREFIX@\bin\libexif-12.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "ufraw-gimp.exe"; DestDir: "{app}\lib\gimp\2.0\plug-ins"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\UFRaw"; Filename: "{app}\bin\ufraw.exe"
Name: "{userdesktop}\UFRaw"; Filename: "{app}\bin\ufraw.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\UFRaw"; Filename: "{app}\bin\ufraw.exe"; Tasks: quicklaunchicon

[Registry]
Root: HKLM; SubKey: "Software\Microsoft\Windows\CurrentVersion\App Paths\ufraw.exe"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\ufraw.exe";
Root: HKLM; SubKey: "Software\Microsoft\Windows\CurrentVersion\App Paths\ufraw.exe"; ValueType: string; ValueName: "Path"; ValueData: "{app}\bin;{reg:HKLM\SOFTWARE\GTK\2.0,DllPath|none}";
Root: HKLM; SubKey: "Software\Microsoft\Windows\CurrentVersion\App Paths\ufraw-batch.exe"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\ufraw-batch.exe";
Root: HKLM; SubKey: "Software\Microsoft\Windows\CurrentVersion\App Paths\ufraw-batch.exe"; ValueType: string; ValueName: "Path"; ValueData: "{app}\bin;{reg:HKLM\SOFTWARE\GTK\2.0,DllPath|none}";

[Run]
Filename: "{app}\bin\ufraw.exe"; Description: "{cm:LaunchProgram,UFRaw}"; Flags: nowait postinstall skipifsilent

[InstallDelete]
Type: files; Name: {%USERPROFILE}\.gimp-2.0\plug-ins\ufraw-gimp.exe; Tasks: unplug
Type: files; Name: {%USERPROFILE}\.gimp-2.2\plug-ins\ufraw-gimp.exe; Tasks: unplug

[messages]
SelectDirDesc=If you have the GIMP installed, [name] should be installed in the GIMP directory. Hopefully, the wizard will guess your GIMP directory correctly.
WelcomeLabel2=%nThis wizard will install [name/ver] on your computer.%n%nYou must install GTK+ before installing [name]. It is also recommended to install the GIMP.  Both packages can be downloaded from http://gimp-win.sourceforge.net/%n%nIt is recommended that you will not run the GIMP application during this installation.
