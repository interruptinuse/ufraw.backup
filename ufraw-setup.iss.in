; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=UFRaw
AppVerName=UFRaw @VERSION@
AppPublisher=Udi Fuchs
AppPublisherURL=http://ufraw.sourceforge.net/
AppSupportURL=http://ufraw.sourceforge.net/
AppUpdatesURL=http://ufraw.sourceforge.net/
DefaultDirName={reg:HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\WinGimp-2.0_is1,InstallLocation|{pf}\UFRaw}
DefaultGroupName={reg:HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\WinGimp-2.0_is1,Inno Setup: Icon Group|UFRaw}
AllowNoIcons=yes
Compression=lzma
SolidCompression=yes
@COMMENT_ICON@SetupIconFile=ufraw_icon.ico
UninstallDisplayIcon={app}\bin\ufraw.exe
DirExistsWarning=no
OutputBaseFileName=ufraw-@VERSION@-setup
OutputDir=.
WizardImageFile=ufraw-setup.bmp
WizardSmallImageFile=ufraw_icon.bmp
UsePreviousAppDir=no
ChangesAssociations=yes

[Tasks]
Name: "unplug"; Description: "Attempt to delete old versions of the UFRaw plug-in"
; that were installed in the user's folders. This option only deletes ufraw-gimp.exe from the current user folders. You should make sure that there are no other copies of ufraw-gimp.exe in any of the user's folders that wish to use the new version of the UFRaw plug-in."
; GroupDescription: "Delete previous plug-in versions:";
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"
; GroupDescription: "{cm:AdditionalIcons}";
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"
; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "UFRawAssociation"; Description: "UFRaw ID files"; GroupDescription: "Associate the following file types with UFRaw:"
Name: "DNGAssociation"; Description: "DNG - Adobe Digital Negative"; GroupDescription: "Associate the following file types with UFRaw:"
Name: "CRWAssociation"; Description: "CRW,CR2 - Canon Raw"; GroupDescription: "Associate the following file types with UFRaw:"
Name: "NEFAssociation"; Description: "NEF - Nikon Electronic Format"; GroupDescription: "Associate the following file types with UFRaw:"
Name: "PEFAssociation"; Description: "PEF - Pentax Electronic Format"; GroupDescription: "Associate the following file types with UFRaw:"
Name: "RAFAssociation"; Description: "RAF - Fuji Raw Format"; GroupDescription: "Associate the following file types with UFRaw:"
Name: "ORFAssociation"; Description: "ORF - Olympus Raw Format"; GroupDescription: "Associate the following file types with UFRaw:"
Name: "MRWAssociation"; Description: "MRW - Minolta Raw"; GroupDescription: "Associate the following file types with UFRaw:"
Name: "ARWAssociation"; Description: "ARW - Sony DSLR Raw"; GroupDescription: "Associate the following file types with UFRaw:"

[Files]
Source: "ufraw.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "ufraw-batch.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "po\ca.mo"; DestDir: "{app}\lib\locale\ca\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\cs.mo"; DestDir: "{app}\lib\locale\cs\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\da.mo"; DestDir: "{app}\lib\locale\da\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\de.mo"; DestDir: "{app}\lib\locale\de\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\es.mo"; DestDir: "{app}\lib\locale\es\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\fr.mo"; DestDir: "{app}\lib\locale\fr\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\it.mo"; DestDir: "{app}\lib\locale\it\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\ja.mo"; DestDir: "{app}\lib\locale\ja\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\ko.mo"; DestDir: "{app}\lib\locale\ko\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\nb.mo"; DestDir: "{app}\lib\locale\nb\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\nl.mo"; DestDir: "{app}\lib\locale\nl\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\pl.mo"; DestDir: "{app}\lib\locale\pl\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\pt.mo"; DestDir: "{app}\lib\locale\pt\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\ru.mo"; DestDir: "{app}\lib\locale\ru\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\sr@latin.mo"; DestDir: "{app}\lib\locale\sr@latin\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\sr.mo"; DestDir: "{app}\lib\locale\sr\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\sr@latin.mo"; DestDir: "{app}\lib\locale\sr@latin\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\sv.mo"; DestDir: "{app}\lib\locale\sv\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "po\zh_CN.mo"; DestDir: "{app}\lib\locale\zh_CN\LC_MESSAGES\"; DestName: "ufraw.mo"; Flags: ignoreversion
Source: "@DOSPREFIX@\bin\liblcms-1-ufraw.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "@DOSPREFIX@\bin\bzip2.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "ufraw-gimp.exe"; DestDir: "{app}\lib\gimp\2.0\plug-ins"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\UFRaw"; Filename: "{app}\bin\ufraw.exe"
Name: "{userdesktop}\UFRaw"; Filename: "{app}\bin\ufraw.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\UFRaw"; Filename: "{app}\bin\ufraw.exe"; Tasks: quicklaunchicon

[Registry]
Root: HKLM; SubKey: "Software\Microsoft\Windows\CurrentVersion\App Paths\ufraw.exe"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\ufraw.exe";
Root: HKLM; SubKey: "Software\Microsoft\Windows\CurrentVersion\App Paths\ufraw.exe"; ValueType: string; ValueName: "Path"; ValueData: "{app}\bin;{reg:HKLM\SOFTWARE\GTK\2.0,DllPath|none}";
Root: HKLM; SubKey: "Software\Microsoft\Windows\CurrentVersion\App Paths\ufraw-batch.exe"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\ufraw-batch.exe";
Root: HKLM; SubKey: "Software\Microsoft\Windows\CurrentVersion\App Paths\ufraw-batch.exe"; ValueType: string; ValueName: "Path"; ValueData: "{app}\bin;{reg:HKLM\SOFTWARE\GTK\2.0,DllPath|none}";

Root: HKCR; Subkey: ".ufraw"; ValueType: string; ValueName: ""; ValueData: "UFRawID"; Flags: uninsdeletevalue; Tasks: UFRAWAssociation
Root: HKCR; Subkey: ".dng"; ValueType: string; ValueName: ""; ValueData: "UFRaw"; Flags: uninsdeletevalue; Tasks: DNGAssociation
Root: HKCR; Subkey: ".crw"; ValueType: string; ValueName: ""; ValueData: "UFRaw"; Flags: uninsdeletevalue; Tasks: CRWAssociation
Root: HKCR; Subkey: ".cr2"; ValueType: string; ValueName: ""; ValueData: "UFRaw"; Flags: uninsdeletevalue; Tasks: CRWAssociation
Root: HKCR; Subkey: ".nef"; ValueType: string; ValueName: ""; ValueData: "UFRaw"; Flags: uninsdeletevalue; Tasks: NEFAssociation
Root: HKCR; Subkey: ".pef"; ValueType: string; ValueName: ""; ValueData: "UFRaw"; Flags: uninsdeletevalue; Tasks: PEFAssociation
Root: HKCR; Subkey: ".raf"; ValueType: string; ValueName: ""; ValueData: "UFRaw"; Flags: uninsdeletevalue; Tasks: RAFAssociation
Root: HKCR; Subkey: ".orf"; ValueType: string; ValueName: ""; ValueData: "UFRaw"; Flags: uninsdeletevalue; Tasks: ORFAssociation
Root: HKCR; Subkey: ".mrw"; ValueType: string; ValueName: ""; ValueData: "UFRaw"; Flags: uninsdeletevalue; Tasks: MRWAssociation
Root: HKCR; Subkey: ".arw"; ValueType: string; ValueName: ""; ValueData: "UFRaw"; Flags: uninsdeletevalue; Tasks: ARWAssociation

Root: HKCR; Subkey: "UFRawID"; ValueType: string; ValueName: ""; ValueData: "UFRawID"; Flags: uninsdeletekey
Root: HKCR; Subkey: "UFRawID"; ValueType: string; ValueName: ""; ValueData: "UFRaw ID file"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "UFRawID\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\ufraw.exe,0"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "UFRawID\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\ufraw.exe"" ""%1"""; Flags: uninsdeletevalue

Root: HKCR; Subkey: "UFRaw"; ValueType: string; ValueName: ""; ValueData: "UFRaw"; Flags: uninsdeletekey
Root: HKCR; Subkey: "UFRaw"; ValueType: string; ValueName: ""; ValueData: "UFRaw photo"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "UFRaw\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\ufraw.exe,0"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "UFRaw\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\ufraw.exe"" ""%1"""; Flags: uninsdeletevalue

[Run]
Filename: "{app}\bin\ufraw.exe"; Description: "{cm:LaunchProgram,UFRaw}"; Flags: nowait postinstall skipifsilent

[InstallDelete]
Type: files; Name: {%USERPROFILE}\.gimp-2.0\plug-ins\ufraw-gimp.exe; Tasks: unplug
Type: files; Name: {%USERPROFILE}\.gimp-2.2\plug-ins\ufraw-gimp.exe; Tasks: unplug
Type: files; Name: {app}\bin\liblcms-1-16-ufraw.dll;

[messages]
SelectDirDesc=[name] should be installed in the GIMP directory. Hopefully, the wizard will guess your GIMP directory correctly.
SelectDirLabel3=Setup will install [name] into the following folder. Do not change this folder unless you really know what you are doing.
WelcomeLabel2=%nThis wizard will install [name/ver] on your computer.%n%nYou must install GIMP (at least version 2.4.6) before installing [name]. GIMP can be downloaded from http://gimp-win.sourceforge.net/%n%nIt is recommended that you will not run the GIMP application during this installation.
SelectTasksLabel2=Select the additional tasks you would like to perform while installing UFRaw:
